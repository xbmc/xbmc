set(PLEXDIR "@PLEXDIR@")
set(ROOTDIR "@ROOTDIR@")
set(DEPENDDIR "@DEPENDDIR@")
set(RESOURCEPATH "@RESOURCEPATH@")
set(BINPATH "@BINPATH@")
set(LIBPATH "@LIBPATH@")
set(ARCH @ARCH@)
set(EXECUTABLE_NAME "@EXECUTABLE_NAME@")

if(NOT WIN32)
  if(APPLE)
    set(TARGET "${EXECUTABLE_NAME}")
  elseif(UNIX)
    set(TARGET "${BINPATH}/${EXECUTABLE_NAME}")
  endif()

  function(gp_item_default_embedded_path_override item default_embedded_path_var)
    set(path "@executable_path")
    if(APPLE)
      set(path "@executable_path/../Frameworks")
    else()
      set(path "@executable_path")
    endif()
    set(${default_embedded_path_var} ${path} PARENT_SCOPE)
  endfunction()

  if(APPLE)
    include(${PLEXDIR}/BundleUtilitiesPlex.cmake)
  else()
    include(BundleUtilities)
  endif()

  set(BU_CHMOD_BUNDLE_ITEMS ON)
  file(GLOB INST_LIBS ${CMAKE_INSTALL_PREFIX}/${LIBPATH}/*${CMAKE_SHARED_LIBRARY_SUFFIX})
  set(plugins "${CMAKE_INSTALL_PREFIX}/${RESOURCEPATH}/XBMC/system/ImageLib-${ARCH}.so")
  message(${CMAKE_INSTALL_PREFIX}/${TARGET}.app)
  fixup_bundle("${CMAKE_INSTALL_PREFIX}/${TARGET}.app" "${plugins};${INST_LIBS}" "${DEPENDDIR}/lib;${DEPENDDIR}/Frameworks;${DEPENDDIR}/lib64" COMPONENT RUNTIME)

  if(APPLE)
    message("Signing binary: ${CMAKE_INSTALL_PREFIX}/${EXECUTABLE_NAME}.app")
    execute_process(COMMAND codesign --force --sign "Developer ID Application: Plex Inc." -r ${PLEXDIR}/Resources/requirement.bin "${CMAKE_INSTALL_PREFIX}/${EXECUTABLE_NAME}.app")
  endif()
else()
  # On win we use this for code signing
  file(GLOB_RECURSE EXES ${CMAKE_INSTALL_PREFIX}/*.exe)
  set(ENV{errorlevel} 1)

  foreach(e ${EXES})
    message("-- Signing: ${PLEXDIR}/scripts/WindowsSign.cmd ${e}")
    execute_process(COMMAND ${PLEXDIR}/scripts/WindowsSign.cmd "${e}" RESULT_VARIABLE RES)
  endforeach()

  file(GLOB_RECURSE DLLS ${CMAKE_INSTALL_PREFIX}/*.dll)
  foreach(d ${DLLS})
    message("-- Signing: ${d}")
    execute_process(COMMAND ${PLEXDIR}/scripts/WindowsSign.cmd "${d}" RESULT_VARIABLE RES)
  endforeach()
endif()

